.type Seq = [h: symbol, t: Seq]

.decl token(i: number, t: symbol)
.input token(IO=file, columns="0:1", delimiter=",")

.decl tokeni(t: symbol, i: number)
tokeni(t, autoinc()) :- token(_, t).

.decl maxi(i: number)
maxi(i) :- i = max n : tokeni(_, n).

.decl sequence(s: Seq, i: number)
sequence([t, nil], i) :- maxi(i), tokeni(t, i).
sequence([t, seq], i-1) :- sequence(seq, i), tokeni(t, i-1).

.decl sequence0(s: Seq)
sequence0(s) :- sequence(s, 0).

// Maybe a .component??
//===============================================================

.type Substantive = Noun | Pronoun
.type Noun <: symbol
.type Pronoun <: symbol
.type Particle <: symbol
.type ConjugatedVerb <: symbol

.type SOV = [s: SubjectPhrase, o: ObjectPhrase, v: VerbPhrase]

.type SP  = [s: SubjectPhrase, p: PredicatePhrase]

.type SubjectPhrase = [n: Substantive, p: Particle]

.type PredicatePhrase = [n: Substantive, v: ConjugatedVerb]

.type ObjectPhrase = [n: Substantive, p: Particle]

.type VerbPhrase = [
  v: ConjugatedVerb
]

/*****
 * Grammatical roles
 *****/

.input particle(IO=file)
.decl particle(token: symbol)

.input pronoun(IO=file)
.decl pronoun(token: symbol)

.decl noun(token: symbol, meaning: symbol)
.input noun(IO=sqlite, dbname="sqlite.db")

.decl verb(lemma: symbol, type: symbol, meaning: symbol)
.input verb(IO=sqlite, dbname="sqlite.db") // facts are read from sqlite.db file

.decl verb_conj(lemma: symbol, masu: symbol, te: symbol)
.input verb_conj(IO=sqlite, dbname="sqlite.db") // facts are read from sqlite.db file

.decl verb_masu(conjugated: symbol)
verb_masu(conjugated) :-
  verb_conj(_, conjugated, _).

.decl verb_stem_ending(lemma: symbol, stem: symbol, ending: symbol)
verb_stem_ending(lemma, stem, ending) :-
  verb(lemma, _, _),
  stem = substr(lemma, 0, strlen(lemma) - 3),
  ending = substr(lemma, strlen(lemma) - 3, 3).

.decl verb_te(conjugated: symbol)
verb_te(cat(stem, "いて")) :-
  verb(lemma, "godan", _),
  verb_stem_ending(lemma, stem, "く").

verb_te(cat(stem, "て")) :-
  verb(lemma, "ichidan", _),
  verb_stem_ending(lemma, stem, "る").

.decl substantive(token: symbol)
substantive(token) :- noun(token, _).
substantive(token) :- pronoun(token).

/*****
 * Capturing statement components
 * ex: watashi ha gakusei desu
 ****/

.decl subject_phrase(sp: SubjectPhrase, start: number, end: number)
subject_phrase(sp, start, end) :-
  sequence([s, [sm, _]], i),
  substantive(s),
  particle(sm),
  sp = [s, sm],
  start = i,
  end = i+2.

.decl predicate_phrase(pred: PredicatePhrase, start: number, end: number)
predicate_phrase(pred, start, end) :-
  sequence([o, [v, _]], i),
  substantive(o),
  verb_masu(v),
  pred = [o, v],
  start = i,
  end = i+2.

.decl object_phrase(op: ObjectPhrase, start: number, end: number)
object_phrase(op, start, end) :-
  sequence([o, [p, _]], i),
  substantive(o),
  particle(p),
  op = [o, p],
  start = i,
  end = i+2.

.decl verb_phrase(vp: VerbPhrase, start: number, end: number)
verb_phrase(vp, start, end) :-
  sequence([v, _], i),
  verb_masu(v),
  vp = [v],
  start = i,
  end = i+1.
verb_phrase(vp, start, end) :-
  sequence([v, _], i),
  verb_te(v),
  vp = [v],
  start = i,
  end = i+1.

.decl sp_statement(sp: SP)
sp_statement(sp) :-
  subject_phrase(s, _, end),
  predicate_phrase(p, end, _),
  sp = [s, p].

.decl sov_statement(sov: SOV)
sov_statement(sov) :-
  subject_phrase(s, _, end1),
  object_phrase(o, end1, end2),
  verb_phrase(v, end2, _),
  sov = [s, o, v].

.output token
.output verb_te
.output sp_statement
.output sov_statement
